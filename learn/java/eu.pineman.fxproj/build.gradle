plugins {
	id 'java'
	id 'application'
	id 'idea'
	id 'org.openjfx.javafxplugin'		version '0.0.8'
	id 'org.beryx.jlink'				version '2.15.1'
	id 'com.github.ben-manes.versions'	version '0.23.0'
}

// Application Configuration
ext {
	appName = "FXApp"
	appLauncher = "FXApp"
	appPackaging = "FXApp"
	appModuleName = "eu.pineman.fxproj"
	appMainClass = "eu.pineman.fxproj.FXApp"
}

mainClassName = "${ext.appModuleName}/${ext.appMainClass}"
version = "0.1.0-SNAPSHOT"

repositories {
	mavenCentral()
}

javafx {
	version = '13.0.1'
	modules = [
		'javafx.controls',
		'javafx.fxml'
	]
}

jlink {
	imageZip = project.file("${buildDir}/distributions/${appPackaging}-${version}-${javafx.platform.classifier}.zip")
	options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
	launcher {
		name = "${appLauncher}"
	}
}

task dist {
	dependsOn clean, jlinkZip
	description "Calls clean and then jlinkZip [default]"
}

defaultTasks 'dist'

if (project.hasProperty('debugRun') && project.debugRun as boolean) {
	run {
		jvmArgs '-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=*:5005'
	}
}

// https://github.com/openjfx/samples/blob/master/CommandLine/Non-modular/Gradle/hellofx/build.gradle
//jar {
//	manifest {
//		attributes 'Main-Class': appMainClass
//	}
//	from {
//		configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
//	}
//}
